import me.modmuss50.mpp.ReleaseType

plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = "${rootProject.mod_version}+mc${rootProject.minecraft_version}"

    var buildNum = rootProject.mod_build_number
    if (!buildNum.isBlank()) {
        version += "-b${buildNum}"
    }
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    ext {
        generatedResources = project(":common").file("src/main/generated").absolutePath
    }

    // Add the generated resources to the main source set
    sourceSets {
        main {
            resources {
                srcDirs += [project.generatedResources]
            }
        }
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.

        // ParchmentMC
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }

        // EMI, ModMenu
        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/"
        }

        // FCAPIP
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }

        // Modrinth
        repositories {
            exclusiveContent {
                forRepository {
                    maven {
                        name = "Modrinth"
                        url = "https://api.modrinth.com/maven"
                    }
                }

                filter {
                    includeGroup "maven.modrinth"
                }
            }
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${parchmentmc_minecraft_version}:${parchmentmc_version}@zip")
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}

publishMods {
    def fabricProject = project(":fabric")
    def neoforgeProject = project(":neoforge")
    def supportedMcVersions = supported_minecraft_versions.split(",")
    def modrinthOptions = modrinthOptions {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.addAll(supportedMcVersions)
        projectId = "WUkCTkrV"
    }

    def curseforgeOptions = curseforgeOptions {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        clientRequired = true
        serverRequired = true
        javaVersions.add(JavaVersion.VERSION_21)
        minecraftVersions.addAll(supportedMcVersions)
        projectId = "1399086"
    }

    displayName = "Magnetizing v${project.version}"
    changelog = rootProject.file("Changelog.md").text
    type = ReleaseType.of(release_type)

    modrinth("modrinthFabric") {
        from modrinthOptions
        file fabricProject
        modLoaders.addAll("fabric", "quilt")

        // Fabric API
        requires {
            id = "P7dR8mSH"
        }

        // Forge Config API Port
        requires {
            id = "ohNO6lps"
        }

        // Architectury
        requires {
            id = "lhGA9TYQ"
        }
    }

    curseforge("curseforgeFabric") {
        from curseforgeOptions
        file fabricProject
        modLoaders.addAll("fabric", "quilt")

        // Fabric API, Forge Config API Port, Architectury
        requires("fabric-api", "forge-config-api-port", "architectury-api")
    }

    modrinth("modrinthNeoForge") {
        from modrinthOptions
        file neoforgeProject
        modLoaders.add("neoforge")

        // Architectury
        requires {
            id = "lhGA9TYQ"
        }
    }

    curseforge("curseforgeNeoForge") {
        from curseforgeOptions
        file neoforgeProject
        modLoaders.add("neoforge")

        // Architectury
        requires("architectury-api")

        // Configured
        optional("configured")
    }

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = "Command17/Magnetizing"
        commitish = branch
        tagName = project.version

        additionalFile(fabricProject)
        additionalFile(neoforgeProject)
    }
}